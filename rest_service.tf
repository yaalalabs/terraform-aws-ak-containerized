# REST Service Module
# Manages the main ECS service that handles HTTP requests

module "rest_service" {
  source = "./modules/rest-service"

  product_alias = var.product_alias
  env_alias     = var.env_alias
  module_name   = var.module_name
  region        = var.region
  prefix        = local.prefix

  vpc_id     = local.vpc_id
  vpc_cidr   = local.vpc_cidr
  subnet_ids = local.subnet_ids

  ecs_cluster_arn = module.ecs.cluster_arn

  service_name   = local.service_name
  container_name = local.container_name

  redis_url                      = local.redis_url
  valkey_url                     = local.valkey_url
  create_dynamodb_memory_table   = var.create_dynamodb_memory_table
  dynamodb_memory_table_arn      = local.dynamodb_memory_table_arn
  dynamodb_memory_table_name     = local.dynamodb_memory_table_name
  create_dynamodb_thread_table   = var.create_dynamodb_thread_table
  dynamodb_thread_table_arn      = local.dynamodb_thread_table_arn
  dynamodb_thread_table_name     = local.dynamodb_thread_table_name
  enable_scheduling              = var.enable_scheduling
  schedule_group_name            = local.schedule_group_name
  scheduler_execution_role_arn   = local.scheduler_execution_role_arn
  create_dynamodb_schedule_table = var.create_dynamodb_schedule_table
  dynamodb_schedule_table_arn    = local.dynamodb_schedule_table_arn
  dynamodb_schedule_table_name   = local.dynamodb_schedule_table_name

  rest_service = {
    cpu                               = var.rest_service.cpu
    memory                            = var.rest_service.memory
    desired_count                     = var.rest_service.desired_count
    container_port                    = var.rest_service.container_port
    health_check_endpoint             = var.rest_service.health_check_endpoint
    health_check_grace_period_seconds = var.rest_service.health_check_grace_period_seconds
    image_uri                         = var.rest_service.image_uri != null ? var.rest_service.image_uri : module.docker_image[0].docker_image_uri
    command                           = var.rest_service.command
    environment_variables             = merge(var.environment_variables, var.rest_service.environment_variables)
  }

  queue_mode                = var.queue_mode
  input_queue_arn           = var.queue_mode ? module.queues[0].input_queue_arn : null
  input_queue_url           = var.queue_mode ? module.queues[0].input_queue_url : null
  output_queue_url          = var.queue_mode ? module.queues[0].output_queue_url : null
  response_store_table_name = (var.queue_mode && !local.is_websocket_mode) ? aws_dynamodb_table.response_store[0].name : null
  queue_config              = var.queue_config

  # WebSocket mode (async / stream): REST service handles connections and push.
  execution_mode                   = var.execution_mode
  websocket_mode                   = local.is_websocket_mode
  websocket_connections_table_name = local.is_websocket_mode ? module.websocket_connections[0].table_name : null
  websocket_connections_table_arn  = local.is_websocket_mode ? module.websocket_connections[0].table_arn : null
  websocket_api_execution_arn      = local.is_websocket_mode ? aws_apigatewayv2_api.ws_api[0].execution_arn : null
  websocket_endpoint_url           = local.is_websocket_mode ? "https://${aws_apigatewayv2_api.ws_api[0].id}.execute-api.${var.region}.amazonaws.com/${local.ws_stage_name}" : null

  tags = var.tags
}

# ECS Cluster

module "ecs" {
  source  = "terraform-aws-modules/ecs/aws"
  version = "6.10.0"

  cluster_name = "${var.product_alias}-${var.env_alias}-${var.module_name}"

  tags = var.tags
}
